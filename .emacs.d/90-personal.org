#+TITLE: Personal

* Header
Enable lexical binding. When visiting the tangled file, enable read
only mode.

#+BEGIN_SRC emacs-lisp
  ;;; 90-personal.el --- Personal configuration -*- lexical-binding: t; buffer-read-only: t; -*-

  ;;; Commentary:

  ;; Personal configuration, me and my computers. Part of Koekelas'
  ;; Emacs configuration.

  ;;; Code:
#+END_SRC

Require libraries.

#+BEGIN_SRC emacs-lisp
  (require 'subr-x)
#+END_SRC

* Name & e-mail
Set name and e-mail.

#+BEGIN_SRC emacs-lisp
  (setq user-full-name "Nicolas De Jaeghere")
  (setq user-mail-address "nicolas@dejaeghe.re")
#+END_SRC

* File & directory names
Declare well known file and directory names.

#+BEGIN_SRC emacs-lisp
  (defconst koek/home-dir-kratos "d:/"
    "File name to home directory on Kratos.")

  (defconst koek/home-dir
    (if (file-exists-p koek/home-dir-kratos)
        koek/home-dir-kratos
      "~/")
    "File name to home directory.")

  (defconst koek/org-dir (expand-file-name "20 Documenten/" koek/home-dir)
    "File name to org directory.")

  (defconst koek/projects-dir (expand-file-name "21 Projecten/" koek/home-dir)
    "File name to projects directory.")

  (defconst koek/static-agenda-files
    (mapcar (lambda (file-name)
              (expand-file-name file-name koek/org-dir))
            '("Inbox.org" "Afspraken.org"))
    "List of file names to static agenda files.")
#+END_SRC

* Agendas
When a file is member of ~koek/static-agenda-files~ or matches pattern
=KOEK_PROJECTS_DIR/PROJECT_NAME/PROJECT_NAME.org=, it's an agenda
file.

#+BEGIN_SRC emacs-lisp
  (use-package org
    :defer t
    :preface
    (defun koek/fetch-directories (file-name &optional full)
      "Return directories in directory FILE-NAME.
  When optional FULL is truthy, return absolute file names."
      (thread-last (directory-files-and-attributes file-name full)
        (seq-filter (pcase-lambda (`(,file-name ,type))
                      (let ((name (file-name-nondirectory file-name)))
                        (and (eq type t)  ; Directory
                             (not (or (string= name ".") (string= name "..")))))))
        (mapcar (lambda (spec)
                  (file-name-as-directory (car spec))))))

    (defun koek/fetch-agenda-files ()
      "Return agenda files."
      (append
       koek/static-agenda-files
       (thread-last (koek/fetch-directories koek/projects-dir 'full)
         (mapcar
          (lambda (file-name)
            (expand-file-name
             (concat (file-name-nondirectory (directory-file-name file-name))
                     ".org")
             file-name)))
         (seq-filter #'file-exists-p))))

    (define-advice
        org-agenda-files (:before (&rest _args) koek/update-agenda-files)
      (setq org-agenda-files (koek/fetch-agenda-files)))
    :config
    (setq org-directory koek/org-dir)
    (setq org-refile-targets
          '((nil . (:maxlevel . 3))       ; Current
            (koek/fetch-agenda-files . (:maxlevel . 3)))))

  (use-package org-capture
    :defer t
    :config
    (setq org-capture-templates
          '(("t" "Task" entry (file+olp "Inbox.org" "Taken")
             "* TODO %?"
             :empty-lines 1)
            ("a" "Appointment" entry (file "Afspraken.org")
             "* %?"
             :empty-lines 1))))
#+END_SRC

* News feeds
Set news feeds.

#+BEGIN_SRC emacs-lisp
  (use-package elfeed
    :defer t
    :preface
    (declare-function elfeed-db-get-feed "elfeed-db")
    (declare-function elfeed-meta--put "elfeed-db")

    (defconst koek/feeds
      '(("Baggers" youtube "UCMV8p6Lb-bd6UZtTc_QD4zA" lisp)
        ("Clickspring" youtube "UCworsKCR-Sx6R6-BnIjS2MA" metalwork)
        ("Clojure" reddit "Clojure" clojure)
        ("ClojureTV" youtube "UCaLlzGqiPE2QRj6sSOawJRg" clojure)
        ("DIY" reddit top "DIY" create)
        ("Documentaries" reddit top "Documentaries" video doc)
        ("Emacs" reddit "emacs" emacs)
        ("Sacha Chua" "http://sachachua.com/blog/category/emacs/feed/" blog emacs)
        ("Two-Bit History" "https://twobithistory.org/feed.xml" blog history)
        ("frank howarth" youtube "UC3_VCOJMaivgcGqPCTePLBA" woodwork))
      "List of news feeds.
  News feed is a list of format:

  (\"title\" \"url\" tag-1 tag-2 tag-n)
  (\"title\" reddit \"id\" tag-1 tag-2 tag-n)
  (\"title\" reddit top \"id\" tag-1 tag-2 tag-n)
  (\"title\" youtube \"id\" tag-1 tag-2 tag-n)")

    (defun koek/add-feed (title url &rest tags)
      "Add news feed to the database.
  TITLE and URL are strings.  TAGS are zero or more symbols."
      (setf (elfeed-meta (elfeed-db-get-feed url) :title) title)
      (push (cons url tags) elfeed-feeds))
    :config
    (mapc (lambda (feed)
            (let ((args
                   (pcase feed
                     (`(,title reddit top ,id . ,tags)
                      `(,(concat title " (Reddit - Top)")
                        ,(format "https://www.reddit.com/r/%s/top.rss?t=month" id)
                        post ,@tags))
                     (`(,title reddit ,id . ,tags)
                      `(,(concat title " (Reddit)")
                        ,(format "https://www.reddit.com/r/%s.rss" id)
                        post ,@tags))
                     (`(,title youtube ,id . ,tags)
                      `(,(concat title " (YouTube)")
                        ,(format "https://www.youtube.com/feeds/videos.xml?channel_id=%s" id)
                        video ,@tags))
                     (_feed
                      feed))))
              (apply #'koek/add-feed args)))
          koek/feeds))
#+END_SRC

* Initial buffer
Show agenda as initial buffer.

#+BEGIN_SRC emacs-lisp
  (defun koek/make-initial-buffer ()
    "Return initial buffer."
    (let ((layout (current-window-configuration)))
      (org-agenda-list)
      (set-window-configuration layout))
    (get-buffer "*Org Agenda*"))

  (setq initial-buffer-choice #'koek/make-initial-buffer)
#+END_SRC

* Footer
#+BEGIN_SRC emacs-lisp
  ;;; 90-personal.el ends here
#+END_SRC
